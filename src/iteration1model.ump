namespace ca.mcgill.ecse223.climbsafe.model;

class ClimbSafe {
  Integer guideWeeklyCost;
  1 <@>- 1 ClimbingSeason season;
  1 <@>- * Booking bookings;
  1 <@>- * Assignment assignments;
  1 <@>- * BookableEquipment bookableEquipments;
  1 <@>- * Hotel hotels;
  1 <@>- * User users;
}

class User {
  abstract;
  unique String email;
  String password;
}

class Administrator {
  isA User;
  email = "admin@nmc.nt"; //should be immutable but umple does not support both immutable and unique
  password = "admin";
}

class Member {
  isA User;
  abstract;
  String name;
  Integer emergencyPhoneNumber;
}

class MountainGuide {
  isA Member;
}

class Climber {
  isA Member;
  1 climber -- 1 Booking booking;
}

class Booking {
  Integer numOfWeeks;
  Boolean wantsGuide;
  Boolean wantsHotel;
  Integer totalCost; //details on the computation are given in the project report
  * bookings -- * BookableEquipment bookedEquipments;
}



class Assignment {
  Date reservationStartDate;
  Date reservationEndDate;
  * assignments -- 0..1 MountainGuide assignedGuide;
  * assignments -- 1..* ClimbingWeek assignedWeeks;
  1 assignment -- 1 Booking booking;
}

class ClimbingSeason {
  Date startDate;
  Date endDate;
  1 season <@>- 1..* ClimbingWeek weeks;
}

class ClimbingWeek {
}

class Hotel {
  String name;
  String address;
  Integer rating; 
  0..1 assignedHotel -- * Assignment assignments;
}

class Equipment {
  isA BookableEquipment;
  Integer weight;
  Integer cost;
}

class BundleItem {
  * bundleItems -- 1 Equipment equipment;
}

class BookableEquipment {
  abstract;
  unique String name;
}

class EquipmentBundle {
  isA BookableEquipment;
  1 bundle <@>- 2..* BundleItem items;
  Integer bundlePercentageDiscount;
}